#!/usr/bin/env python
import os

Import("env")
Import("env_modules")

is_gcc = "gcc" in os.path.basename(env["CC"])

module = os.path.splitext(os.path.basename(os.path.realpath(".")))[0]

env_module = env_modules.Clone()
env_module.Append(CPPPATH=[".", "raknet/Source"])

sources = Glob("*.cpp")
sources += Glob("raknet/Source/*.cpp")

if env["platform"] == "windows":
	env_module.Append(CPPDEFINES=["_CRT_SECURE_NO_WARNINGS", "_WINSOCK_DEPRECATED_NO_WARNINGS"])

if is_gcc:
	env_module.Append(CPPFLAGS=["-Wno-array-bounds"]) # too many false errors

# Compile as a static library
lib = env_module.Library(module, sources)
# Add the library as a dependency of the final executable
env.Prepend(LIBS=[lib])
